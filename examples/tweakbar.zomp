///
/// Demonstrate usage of AntTweakBar in Zomp
///

requireLibs opengl20 glfw glutils libcee anttweakbar

var AppInfo* app nullptr(AppInfo)

func void onWindowResize(int newWidth, int newHeight):
  autoSetupViewport()
  glfwGetWindowSize &app*.windowWidth &app*.windowHeight

  TwWindowSize(newWidth, newHeight)
  return
end

macro doTrace sectionName code:
  sectionNameStr := cstring:quote sectionName.id
  ret $:
    println "Starting section " #sectionNameStr
    #code
    println "Done section " #sectionNameStr
  end
end

func void customInit():
  if (TwInit(TW_OPENGL, nullptr void) == 0):
    println "AntTweakBar initialization failed, exiting"
    glfwTerminate()
    exit 2
  end

  doTrace InitTweakbar:
    glfwSetWindowSizeCallback (cast void* &onWindowResize)
    glfwSetMouseButtonCallback (cast void* &TwEventMouseButtonGLFW)
    glfwSetMousePosCallback (cast void* &TwEventMousePosGLFW)
    glfwSetMouseWheelCallback (cast void* &TwEventMouseWheelGLFW)
    glfwSetKeyCallback (cast void* &TwEventKeyGLFW)
    glfwSetCharCallback (cast void* &TwEventCharGLFW)
  end
end

func int main():
  init app:
    app*.fsaaSamples = 4
    app*.customInit = &customInit
  end

  angle := 0.0
  speed := 128.0

  mainloop:
    // Need to move this into mainloop to be able to access time.
    // TODO: consider something like glfunc which has a mainloop built in.
    if (frameNum == 0):
      doTrace SetupTweakbars:
        bar := TwNewBar "ZompBar"

        TwDefine " GLOBAL help='Demonstrating integration of AntTweakBar and Zomp'"
        TwAddVarRW bar "speed" TW_TYPE_FLOAT (cast void* &speed) " label='Speed' precision=1"
        TwAddVarRO bar "time" TW_TYPE_FLOAT (cast void* &time) " label='Time' precision=1"
      end
    end
    angle = angle + speed * frameDuration

    glClearColor 0. 0. 0. 0.
    glClear(GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT)

    glMatrixMode GL_PROJECTION
    glLoadIdentity()
    aspect := toDouble(app*.windowWidth) / toDouble(app*.windowHeight)
    gluPerspective( 90.0d, aspect, 1.0d, 200.0d )

    glMatrixMode GL_MODELVIEW
    glLoadIdentity()
    glTranslatef 0. -4. -20.0
    glRotatef angle 0. 1. 0.

    drawOrientationGrid()

    TwDraw()
  end

  TwDeleteAllBars()

  drawPausedOverlay()
  glfwSwapBuffers()

  return 0
end

