/**
 * Libraries providing a C like feel

!verify off
!syntax indent
 */


///-----------------------------------------------------------------------------
/// Section: Untested
///-----------------------------------------------------------------------------

///
/// Declares a function. Use this to prevent emacs indentation in next line :)
///
template funcd rettype nameAndArgs
  func #rettype #nameAndArgs
end

template macro:failUnless cond
  unless #{cond}
    ret ${error expression is false #{cond}}
  end
end

macro uniqueIds purpose ids...
  var ast* code ${seq}

  forEachAstChild child ids
    ast:addChild code ${uniqueIds #child #purpose}
  end

  ret code
end

/// if statement
///
/// Usage example:
///
/// if a > 10 then
///   print "It's bigger!"
/// else if a < 5 then
///   print "It's smaller!"
/// else
///   print "Neither to big nor to small. Purrfect"
/// end
macro if2 cond then onTrue remArgs...
  macro:failUnless cstring:equal(getField(then, id), "then")

  uniqueId endLabel "if_end"

  var astp code ${seq}

  ast:addChild code ${when #cond
    #onTrue
    branch #endLabel
  end}

  var int currentArg 0
  const int argCount getField(remArgs, childCount)

  while (currentArg+3 <= argCount)
    var astp econd ast:child(remArgs, currentArg+1)
    var astp eexpr ast:child(remArgs, currentArg+2)

    ast:addChild code ${seq
      when #econd
        #eexpr
        branch #endLabel
      end
    end}

    currentArg = currentArg + 3
  end

  when (currentArg+1 < argCount)
    var astp elseCode ast:child(remArgs, currentArg+1)

    ast:addChild code elseCode
  end

  ast:addChild code ${label #endLabel}

  ret code
end

template op| l r
  int:or #l #r
end

template op& l r
  int:and #l #r
end

macro op|| l r
  uniqueId result "||"

  ret ${
    #result := false
    if2 #l then
      #result = true
    elseif #r
      #result = true
    end
    #result
  end}
end

macro op&& l r
  uniqueId result "&&"

  ret ${
    #result := false
    if2 #l then
      #result = #r
    end
    #result
  end}
end

template op==_str left right
  strcmp(#left, #right) == 0
end

template op!=_str left right
  strcmp(#left, #right) != 0
end

macro op:= name value
  if2 (getField(name, childCount) == 0) then
    ret ${var2 #name #value}
  end
  ret ${error "Expected a single identifier on left hand side" ${#name := #value}}
end

// macro dummy l r
//   printlnString "applying dummy"
//   ret ${foo bar}
// end
// 
// func void p2(ast* l, ast* r)
//   printlnString "should do something now.."
// end

/// Given an ast and an ast containing placeholders _/__ the
/// shapes of both ast's are matched against each other. They match if they have
/// the same name, same number of children and all their children are matching.
///
/// The _ placeholder matches expressions without children (like 'a') while
/// the __ placeholder matches any other ast
/// _... as last argument means any number of parameters
///
/// Examples:
///   ast:matchesShape ${foo bar} ${foo bar} => true
///   ast:matchesShape ${a b c} ${__} => true
///   ast:matchesShape ${foo (some child) bar} ${foo __ bar} => true
///   ast:matchesShape ${a b} ${a} => false
///   ast:matchesShape ${a b c} ${a b} => false
///   ast:matchesShape ${a b} ${a b _} => false
///   ast:matchesShape ${a _...} ${a 1 2 3 4} => true
///
func bool ast:matchesShape(ast* codeArg, ast* templateArg)
  var ast* code codeArg
  var ast* template templateArg

  assert (template !=_ptr nullptr ast)
  assert (code !=_ptr nullptr ast)

  var cstring templateId getField(template, id)
  var int templateChildCount getField(template, childCount)
  var cstring codeId getField(code, id)
  var int codeChildCount getField(code, childCount)

  when (bool:and (templateId ==_str "_") (codeChildCount == 0))
    ret true
  end

  when (templateId ==_str "__")
    ret true
  end

  varargs := false
  lastChild := nullptr ast
  if2 (templateChildCount > 0) then
    lastChild = ast:child template (templateChildCount - 1)
    assert (lastChild !=_ptr nullptr ast)
    if2 ((getField(lastChild, childCount) == 1) && (getField(lastChild, id) ==_str "postop...")) then
      var ast* lastChildChild ast:child(lastChild, 0)
      varargs = (getField(lastChildChild, id) ==_str "_")
    end
  end

  if2 varargs then
    if2 ((codeChildCount >= templateChildCount - 1) && (codeId ==_str templateId)) then
      for childNum2 0 (templateChildCount - 1)
        unless (ast:matchesShape (ast:child code childNum2) (ast:child template childNum2))
          ret false
        end
      end
      ret true
    end
  else
    if2 ((codeChildCount == templateChildCount) && (codeId ==_str templateId)) then
      for childNum 0 codeChildCount
        unless (ast:matchesShape (ast:child code childNum) (ast:child template childNum))
          ret false
        end
      end
      ret true
    end
  end

  ret false
end

///
/// Will return code to get the address of the denoted element
/// Accepts code of the form id(.id)*
/// Also accepts array expressions like foo[10]
/// Will probably evolve into a general way to get the address of a
/// left hand side expression
///
/// Example:
///   getMemberAddress(${a.b.c}) => (fieldptr (fieldptr a b) c)
///
func ast* getMemberAddress(ast* code)
  if2 (getField code childCount == 0) then
    ret ${ptr #code}
  elseif ast:matchesShape(code, ${_._})
    recordExpr := ast:child(code, 0)
    fieldName := getField ast:child(code, 1) id
    ret ${fieldptr #recordExpr #fieldName}
  elseif ast:matchesShape(code, ${__._})
    nestedPtr := getMemberAddress ast:child(code, 0)
    fieldName2 := getField ast:child(code, 1) id
    ret ${fieldptr #nestedPtr #fieldName2}
  elseif ast:matchesShape(code, ${__._[__]})
    structExpr := ast:child code 0
    memberName := getField ast:child(ast:child(code, 1), 0) id
    indexExpr := ast:child(ast:child(code, 1), 1)
    memberPtr := getMemberAddress(${#structExpr.#memberName})
    ret ${ptradd (load #memberPtr) #indexExpr}
  elseif ast:matchesShape(code, ${_[__]})
    arrayExpr := ast:child code 0
    indexExpr2 := ast:child code 1
    arrayPtr := getMemberAddress(arrayExpr)
    ret ${ptradd (load #arrayPtr) #indexExpr2}
  elseif ast:matchesShape(code, ${__*})
    ptrExpr := ast:child code 0
    ret ${load #ptrExpr}
  end
  ret ${error "Do not know how to get address of this expression" #code}
end

///TODO: add support to call methods
macro op. record component
  addr := getMemberAddress ${#record.#component}

  ret ${load #addr}
end

/// Assigning values. Supports
/// a = b - for first class types
/// a.x = b - for struct members
/// ptr[10] = b - for pointers
macro op= l r
  if2 ast:matchesShape(l, ${_[_]}) then
    var ast* array ast:child(l, 0)
    var ast* index ast:child(l, 1)
    ret ${store ptradd(#array, #index) #r}
  elseif ast:matchesShape(l, ${__.__})
    laddr := getMemberAddress(l)
    ret ${store #laddr #r}
  elseif ast:matchesShape(l, ${__*})
    addr := ast:child l 0
    ret ${store #addr #r}
  else
    ret ${assign #l #r}
  end

  ret ${internal error}
end

/// Accessing pointers like C arrays
template postop[] array index
  load(ptradd #array #index)
end

template op+_ptr ptr offset
  ptradd #ptr #offset
end

template zmp:array:get array index
  load ptradd(zmp:array:addr &(#array), #index)
end

template zmp:array:set array index value
  store ptradd(zmp:array:addr &(#array), #index) #value
end

/// Allocate storage and initialize variable
/// `new Foo a b c' does the same as `Foo:init(malloc Foo, a, b, c)'
macro new type args...
  uniqueId name "new"

  var ast* initCall ast:fromString(cstring:concat(getField(type, id), ":init"))
  ast:addChild initCall ast:fromString(name)
  forEachAstChild arg args
    ast:addChild initCall arg
  end

  var ast* code ${seq}
  ast:addChild code ${var (ptr #type) #name (malloc #type)}
  ast:addChild code initCall
  ast:addChild code ast:fromString(name)

  ret code
end

func void free(void* ptr)
template dealloc ptr free(cast void* #ptr)

template return value
  ret #value
end

template op++ lstring rstring
  cstrings:append #lstring #rstring
end

template preop++ int
  #int = #int + 1
  #int
end

template preop-- int
  #int = #int - 1
  #int
end

template postop++ int
  #int = #int + 1
  #int - 1
end

template postop-- int
  #int = #int - 1
  #int + 1
end

macro preop& obj
  code := getMemberAddress obj
  ret code
end

template preop* ptr
  (load #ptr)
end

template postop* ptr
  (load #ptr)
end

template isNotNull ptr
  #ptr !=_ptr nullptr void
end

template isNull ptr
  #ptr ==_ptr nullptr void
end

macro struct name members
  unless (name.childCount == 0)
    ret ${error "Expected id" #name}
  end

  decl := ast:fromString "type"
  ast:addChild decl name

  forEachAstChild memberList members
    if2 ast:matchesShape(memberList, ${__ _...}) then
      memberType := ast:child memberList 0
      for memberNum 1 memberList.childCount
        memberName := ast:child memberList memberNum
        ast:addChild decl ${#memberType #memberName}
      end
    else
      ret ${error "Expected 'typeExpr id..." #memberList}
    end
  end

  ret decl
end

func bool isMultipleOf(int x, int m)
  ret (int:mod(x,m) == 0)
end

macro ast:foreach3 t0 t1 t2 tree code
  if2 (bool:not isMultipleOf(tree.childCount, 3)) then
    ret ${error "Can only iterate over 3-tuples if child count is a multiple of 3" tree}
  end

  uniqueId childnum "ast:foreach3"

  ret ${
    #childnum := 0
    while (#childnum + 2 < #tree.childCount)
      #t0 := ast:child #tree (#childnum+0)
      #t1 := ast:child #tree (#childnum+1)
      #t2 := ast:child #tree (#childnum+2)
      #code
      #childnum = #childnum + 3
    end
  end}
end

///
/// Will transform an expression of the form (<jux someId (<arg 0>) ..>) to
/// (<someId (<arg 0>) ...>)
///
func ast* ast:shiftLeft(ast* code)
  assert (code.childCount >= 1)
  assert (getField ast:child(code,0) childCount == 0)

  firstChild := ast:child code 0
  newcode := ast:fromString firstChild.id
  for i 1 code.childCount
    ast:addChild newcode ast:child(code,i)
  end
  ret newcode
end

///
/// switch statement
///
/// Example:
///
/// switch _intExpression
///   void
/// case _intExpression
///   _code
/// ...
/// end
///
macro switch intvalue default cases...
  uniqueId value "the_value"

  first := true
  code := ${if2 false then
    println()
  end}

  ast:foreach3 case caseval casecode cases
    unless ast:matchesShape(case, ${case})
      caseNode := ast:shiftLeft ${#case #caseval #casecode}
      ret ${error "Expected 'case VAL EXPR'" #caseNode}
    end

    ast:addChild code $elseif
    ast:addChild code ${#value == #caseval}
    ast:addChild code casecode
  end

  code = ${
    var int #value #intvalue
    #code
  end}

  ret code
end

///-----------------------------------------------------------------------------
/// Section: Overloaded operators
///-----------------------------------------------------------------------------

func ast* defineOpCode(cstring type, cstring rettype, cstring opr, cstring func)
  funcname := type ++ ":" ++ func
  oprname := opr ++ "_" ++ type ++ "_" ++ type

  ret ${
    func #rettype #oprname(#type l, #type r)
      #funcname(l, r)
    end
  end}
end

alias u32:greater u32:sgreater l r
alias u32:greaterEqual u32:sgreaterEqual l r
alias u32:less u32:sless l r
alias u32:lessEqual u32:slessEqual l r
alias u32:div u32:sdiv l r

alias float:div float:fdiv l r
alias float:greater float:ogreater l r
alias float:greaterEqual float:ogreaterEqual l r
alias float:less float:oless l r
alias float:lessEqual float:olessEqual l r

alias double:equal double:oequal l r
alias double:notEqual double:onotEqual l r
alias double:div double:fdiv l r
alias double:greater double:ogreater l r
alias double:greaterEqual double:ogreaterEqual l r
alias double:less double:oless l r
alias double:lessEqual double:olessEqual l r

macro overloadedOps type
  macro defop_ rettype opr name
    oprName := cstring:quote opr.id
    funcName := cstring:quote name.id

    ret ${
      ast:addChild code (defineOpCode type.id #rettype #oprName #funcName)
    end}
  end

  code := ${}

  defop_ "bool" op== equal
  defop_ "bool" op!= notEqual
  defop_ "bool" op> greater
  defop_ "bool" op>= greaterEqual
  defop_ "bool" op< less
  defop_ "bool" op<= lessEqual

  defop_ type.id op+ add
  defop_ type.id op- sub
  defop_ type.id op* mul
  defop_ type.id op/ div

  ret code
end

overloadedOps u32
overloadedOps float
overloadedOps double

template op+ l r
  zmp:cee:add #l #r
end

template op- l r
  zmp:cee:sub #l #r
end

template op* l r
  zmp:cee:mul #l #r
end

template op/ l r
  zmp:cee:div #l #r
end

template op== l r
  zmp:cee:equal #l #r
end

template op!= l r
  zmp:cee:notEqual #l #r
end

template op> l r
  zmp:cee:greater #l #r
end

template op>= l r
  zmp:cee:greaterEqual #l #r
end

template op< l r
  zmp:cee:less #l #r
end

template op<= l r
  zmp:cee:lessEqual #l #r
end

///-----------------------------------------------------------------------------
/// Section: Overloaded printing
///-----------------------------------------------------------------------------

func void print_u32(int i)
  printInt i
end

func void print_float(float f)
  printFloat f
end

func void print_char(char c)
  printChar c
end

func void print_bool(bool b)
  printBool b
end

func void print_double(double d)
  printDouble d
end

func void "print_char*"(char* string)
  printString string
end

func void "print_ast*"(ast* code)
  ast:print code 0
end

macro print args...
  var astp code ${}

  forEachAstChild arg args
    ast:addChild code ${zmp:cee:print #arg}
  end

  ret code
end

macro printnl args...
  var astp code ${}

  forEachAstChild arg args
    ast:addChild code ${zmp:cee:print #arg}
  end

  ast:addChild code ${println()}

  ret code
end

func void printBits(int bitmask)
  printString "0b"
  for i 0 32
    bitnum := 31 - i
    mask := int:shl 1 bitnum
    bit := int:lshr (bitmask & mask) bitnum
    printInt bit
  end
end

///-----------------------------------------------------------------------------
/// Section: Overloaded conversion
///-----------------------------------------------------------------------------

alias toFloat zmp:cee:toFloat x
alias toInt zmp:cee:toInt x
alias toChar zmp:cee:toChar x
alias toCString zmp:cee:toCString x

func float toFloat_u32(u32 i)
  int:toFloat i
end

func int toInt_float(float f)
  float:toInt f
end

func int toInt_double(double d)
  double:toInt d
end

