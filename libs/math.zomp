/**
 * Common math utilities for 3d graphics

!verify off
 */

requireLibs libcee

///-----------------------------------------------------------------------------
/// Section: Constants
///-----------------------------------------------------------------------------

var float math:e 2.71828182845904523536028747135266250
var float math:log2e 1.44269504088896340735992468100189214
var float math:log10e 0.434294481903251827651128918916605082
var float math:ln2 0.693147180559945309417232121458176568
var float math:ln10 2.30258509299404568401799145468436421
var float math:pi 3.14159265358979323846264338327950288
var float math:pi_2 1.57079632679489661923132169163975144
var float math:pi_4 0.785398163397448309615660845819875721
var float math:sqrt2 1.41421356237309504880168872420969808
var float math:sqrt1_2 0.707106781186547524400844362104849039

///-----------------------------------------------------------------------------
/// Section: Util functions
///-----------------------------------------------------------------------------

func float sqrf(float x):
  x * x
end

func float posmod(float x, float divisor):
  mod := fmodf(x, divisor)

  if (mod < 0.0):
    mod = divisor - float:neg(mod)
  end

  ret mod
end

func float powf(float base, float exp)
func double pow(double base, double exp)

func int int:pow(int base, int exp):
  v := base

  remMults := exp

  while (remMults > 1):
    v = v * base
    remMults = remMults - 1
  end

  ret v
end

func float float:mix(float x, float a, float b):
  ret ( (1.0 - x) * a + x * b )
end

unittest:testCasesFor float:mix:
  float:mix 0.0 1.0 10.0 == 1.0
  float:mix 1.0 0.0 4.0 == 4.0
  float:mix 0.5 0.0 4.0 == 2.0
  float:mix 0.25 0.0 8.0 == 2.0
end

unittest:testCasesFor posmod:
  posmod(0.0,2.0) == 0.0
  posmod(1.0,2.0) == 1.0
  posmod(2.0,2.0) == 0.0

  posmod(-1.0,3.0) == 2.0
  posmod(-2.0,3.0) == 1.0

  posmod(23.0, 4.0) == 3.0
  posmod(-61.0, 3.0) == 2.0
end

///-----------------------------------------------------------------------------
/// Section: vector math
///-----------------------------------------------------------------------------

struct v3f:
  float x y z
end

func v3f mv3f(float x, float y, float z):
  ret (structlit v3f x x y y z z)
end

func void v3f:print(v3f this):
  print "v3f(x=" this.x ", y=" this.y ", z=" this.z ")"
end

ofunc void print(v3f this):
  v3f:print this
end

func float v3f:length(v3f this):
  ret sqrtf(sqrf this.x + sqrf this.y + sqrf this.z)
end

func void v3f:set(v3f* this, float x, float y, float z):
  this.x = x
  this.y = y
  this.z = z
end

ofunc bool op==(v3f l, v3f r):
  ret ((l.x==r.x) && (l.y==r.y) && (l.z==r.z))
end

ofunc v3f op+(v3f l, v3f r):
  ret (structlit v3f x (l.x+r.x) y (l.y+r.y) z (l.z+r.z))
end

ofunc v3f op-(v3f l, v3f r):
  ret (structlit v3f x (l.x-r.x) y (l.y-r.y) z (l.z-r.z))
end

func float op*_dot(v3f l, v3f r):
  ret (l.x*r.x + l.y*r.y + l.z*r.z)
end

func v3f op*_cross(v3f l, v3f r):
  ret (structlit v3f x (l.y*r.z - l.z*r.y) y (l.z*r.x - l.x*r.z) z (l.x*r.y - l.y*r.x))
end

ofunc v3f op*(v3f l, v3f r):
  ret (structlit v3f x (l.x * r.x) y (l.y * r.y) z (l.z * r.z))
end

ofunc v3f op*(v3f v, float factor):
  ret (structlit v3f x (v.x * factor) y (v.y * factor) z (v.z * factor))
end

ofunc v3f op*(float factor, v3f v):
  ret (v * factor)
end

ofunc v3f op/(v3f v, float divisor):
  ret (v * (1.0 / divisor))
end

func v3f v3f:normalized(v3f v):
  l := 1.0 / v3f:length(v)
  ret mv3f(v.x * l, v.y * l, v.z * l)
end

func void v3f:makeNormalized(v3f* pv):
  l := 1.0 / v3f:length pv*
  pv.x = pv.x * l
  pv.y = pv.y * l
  pv.z = pv.z * l
end

func v3f v3f:lerp(float t, v3f a, v3f b):
  ret ((1. - t) * a + t * b)
end

/// rotate two dense matrices of size (resultRows,size), (size, resultColumns) and stores result in
/// matrix of size (resultRows, resultColumns)
func void matrixMult(float* result, int resultRows, int resultColumns, int side, float* lhs, float* rhs):
  lhsRows := resultRows
  lhsColums := side
  rhsRows := side
  rhsColumns := resultColumns

  pos := result
  for row 0 resultRows:
    for column 0 resultColumns:
      c := 0.0
      for s 0 side:
        l := (lhs + row * lhsColums + side)*
        r := (rhs + side * rhsColumns + column)*
        c += l * r
      end
      pos* = c
    end
  end
end

///-----------------------------------------------------------------------------
/// Section: rigid body transformations
///-----------------------------------------------------------------------------

struct RBTransform:
  v3f origin
  v3f xaxis yaxis zaxis
  float scale
end

func void RBTransform:print(RBTransform* this, int indent):
  printIndent indent
  print "RBTransform:"
  if (this.scale != 1.0) print(" scale")
  if (this.origin != mv3f 0. 0. 0.) print(" pos")
  if (this.xaxis != mv3f 1. 0. 0. ||
      this.yaxis != mv3f 0. 1. 0. ||
      this.zaxis != mv3f 0. 0. 1.)
    print(" rot")
  println()
  printIndent indent
  println "  xaxis = " this.xaxis
  printIndent indent
  println "  yaxis = " this.yaxis
  printIndent indent
  println "  zaxis = " this.zaxis
  printIndent indent
  println "  origin = " this.origin
  printIndent indent
  println "  scale  = " this.scale
end

ofunc void print(RBTransform t):
  RBTransform:print &t 0
end

func void makeOrigin(RBTransform* this):
  this.origin = mv3f 0. 0. 0.
  this.xaxis = mv3f 1. 0. 0.
  this.yaxis = mv3f 0. 1. 0.
  this.zaxis = mv3f 0. 0. 1.
  this.scale = 1.0
end

func void RBTransform:init(RBTransform* this):
  makeOrigin this
end

func void RBTransform:makeNormalized(RBTransform* this):
  v3f:makeNormalized &this.xaxis
  v3f:makeNormalized &this.yaxis
  v3f:makeNormalized &this.zaxis
end

func void RBTransform:setAxisScales(RBTransform* this, v3f lengths):
  v3f:makeNormalized &this.xaxis
  this.xaxis = this.xaxis * lengths.x
  v3f:makeNormalized &this.yaxis
  this.yaxis = this.yaxis * lengths.y
  v3f:makeNormalized &this.zaxis
  this.zaxis = this.zaxis * lengths.z
end

func RBTransform RBTransform:identity():
  var RBTransform t
  makeOrigin &t
  ret t
end

///-----------------------------------------------------------------------------
/// Section: triangles
///-----------------------------------------------------------------------------

func float triangleArea(v3f v1, v3f v2, v3f v3):
  a := v1 *_cross v2
  b := v2 *_cross v3
  c := v3 *_cross v1

  ret (0.5 * v3f:length(a+b+c))
end

///-----------------------------------------------------------------------------
/// Section: bounding boxes
///-----------------------------------------------------------------------------

struct AABB:
  v3f min max
end

func void AABB:init(AABB* this):
  inf := powf(2.0, 128.0)
  this.min.x = inf
  this.min.y = inf
  this.min.z = inf
  this.max.x = -inf
  this.max.y = -inf
  this.max.z = -inf
end

func void AABB:print(AABB* this):
  print "AABB(min: x=" this.min.x ", y=" this.min.y ", z=" this.min.z
  print " max: x=" this.max.x ", y=" this.max.y ", z=" this.max.z ")"
end

func void AABB:diagonal(v3f* result, AABB* this):
  result.x = this.max.x - this.min.x
  result.y = this.max.y - this.min.y
  result.z = this.max.z - this.min.z
end

func float AABB:diagonalLength(AABB* this):
  var v3f diag
  AABB:diagonal &diag this
  ret v3f:length(diag)
end

func v3f AABB:center(AABB* this):
  ret (0.5 * (this*.min + this*.max))
end

ofunc void print(AABB this):
  print "AABB(" this.min ", " this.max ")"
end

///-----------------------------------------------------------------------------
/// Section: coordinate systems
///-----------------------------------------------------------------------------

struct OrthoBasis3D:
  v3f xaxis yaxis zaxis
end

ofunc void print(OrthoBasis3D basis):
  println "OrthoBasis3D"
  println "  xaxis = " basis.xaxis
  println "  yaxis = " basis.yaxis
  println "  zaxis = " basis.zaxis
end

func OrthoBasis3D makeOrthoBasis(v3f vec):
  var OrthoBasis3D basis
  var v3f v

  xa := fabsf vec.x
  ya := fabsf vec.y
  za := fabsf vec.z

  if ((xa <= ya) && (xa <= za)):
    println "x"
    v = mv3f 0. float:neg(vec.z) vec.y
  elseif ((ya <= xa) && (ya <= za)):
    println "y"
    v = mv3f float:neg(vec.z) 0. vec.x
  elseif ((za <= xa) && (za <= ya)):
    println "z"
    v = mv3f float:neg(vec.y) vec.x 0.
  end

  println v

  basis.xaxis = vec
  basis.yaxis = v * (1.0 / v3f:length v)
  basis.zaxis = basis.xaxis *_cross basis.yaxis

  ret basis
end

///-----------------------------------------------------------------------------
/// Section: compatibility
///-----------------------------------------------------------------------------

// deprecated
struct float3:
  float x y z
end

func void float3:print(float3* this):
  print "float3(x=" this.x ", y=" this.y ", z=" this.z ")"
end

func float float3:length(float3* this):
  ret sqrtf(sqrf this.x + sqrf this.y + sqrf this.z)
end

