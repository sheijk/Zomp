///
/// Tools for writing and running unit tests.
///

requireLibs libcee

var int unittest:failedCount 0
var int unittest:testCount 0

///
/// An assertion macro which detects certain kinds of expressions and inserts
/// code printing the arguments for better error messages
///
/// Example:
/// x := 20.0
/// unittest:assert (sin 10.0 ==_f 20.0)
///
/// Will result in:
///
/// Assertion failed
/// left = -0.544021
/// right = 20.000000
/// op==_f [2 childs]
///   opjux [2 childs]
///     sinf [0 childs]
///     10.0 [0 childs]
///   x [0 childs]
///
macro unittest:assert testCode:
  isCompare := isCompareCode(testCode)

  if isCompare:
    uniqueId lvar "lvar"
    uniqueId rvar "rvar"
    compareF := testCode.id
    l := ast:child(testCode, 0)
    r := ast:child(testCode, 1)
    ret $:
      #lvar := #l
      #rvar := #r
      unless (#compareF #lvar #rvar):
        ++unittest:failedCount
        printHLine '-' 40
        printlnString "Assertion failed"
        printString "left = "
        print #lvar
        println()
        printString "right = "
        print #rvar
        println()
        ast:print ${#testCode} 0
      end
    end
  else:
    ret $:
      unless #testCode:
        ++unittest:failedCount
        printHLine '-' 40
        printlnString "Assertion failed"
        ast:print ${#testCode} 0
      end
    end
  end

  ret ${}
end

alias uassert unittest:assert testCode

func int unittest:printReport():
  if (unittest:failedCount > 0):
    println unittest:failedCount " tests failed"
    ret 1
  end

  println "All " unittest:testCount " unit tests succeeded"
  ret 0
end

struct TestCase:
  cstring name
  cstring functionName
  // (fptr void) function
  TestCase* next
end

var TestCase* unittest:testCases null

func void TestCase:print(TestCase* this):
  printString "TestCase(name="
  printString getField(this,name)
  printString ", functionName="
  printString getField(this,functionName)
  printlnString ")"
end

func void TestCase:printAll(TestCase* this):
  unless (this == nullptr TestCase):
    TestCase:print this
    TestCase:printAll getField(this,next)
  end
end

func int TestCase:count(TestCase* this):
  var TestCase* current this
  var int count 0
  while (current != nullptr TestCase):
    assign count (count + 1)
    assign current getField(current,next)
  end
  ret count
end

macro unittest:testCaseCount:
  ret ast:fromInt(TestCase:count unittest:testCases)
end

func TestCase* unittest:registerTestCase(cstring name):
  var TestCase* case malloc(TestCase)

  setField case name name
  setField case functionName ("testcase_" ++ name ++ int:toCString newUniqueId())
  setField case next unittest:testCases
  assign unittest:testCases case

  ret case
end

macro unittest:testCase name code:
  case := unittest:registerTestCase(name.id)
  funcName := (getField case functionName)
  funcDef := $:
    func void #funcName():
      ++unittest:testCount
      #code
    end
  end
  if std:vm:isInteractive:
    quotedFuncName := cstring:quote funcName
    ret $:
      func void test():
        println "Last compilation of unit test failed!"
      end
      #funcDef
      std:base:run:
        println "Interactive, running unit test function " #quotedFuncName
        #funcName()
      end
    end
  end
  ret funcDef
end

macro unittest:runAllTestCases:
  var ast* code ${}
  var TestCase* case unittest:testCases
  while (case != nullptr TestCase):
    var cstring functionName getField(case, functionName)
    ast:addChild code ${#functionName()}

    assign case getField(case, next)
  end

  ret code
end


template unittest:main extraCode...:
  func int main():
    #extraCode
    unittest:runAllTestCases
    ret unittest:printReport()
  end
end

/// Calls unittest:assert on all childs of the passed
/// sequence expression
macro unittest:assertAll checks:
  var (ptr ast) code ${}
  forEachAstChild check checks:
    when (cstring:equal (getField check id) "opseq"):
      printlnString "Warning: unittest:assertAll found opseq expression. This might indicate using this from indent syntax code which is not supported"
    end
    ast:addChild code ${unittest:assert #check}
  end

  ret code
end


/// Will define a function name_tests_nr which will
/// call all unittest:assert on all given testCases
macro unittest:testCasesFor name testCases...:
  var cstring testName (cstring:concat "test_" (getField name id) "_" (u32:toCString newUniqueId()))

  var astp impl (ast:fromString "unittest:assertAll")
  forEachAstChild testCase testCases:
    ast:addChild impl testCase
  end
  var ast* decl ast:fromString("opcall")
  ast:addChild decl ast:fromString(testName)
  var astp funcDecl ${std:base:func void #decl:
    #impl
  end}

  ret funcDecl
end

std:base:func cstring unittest:newTestFuncNameFor(cstring funcName):
  cstring:concat "test_" funcName "_" (u32:toCString newUniqueId())
end

macro unittest:testFunc funcName impl:
  var cstring testFuncName (unittest:newTestFuncNameFor (getField funcName id))
  var ast* decl ast:fromString("opcall")
  ast:addChild decl ast:fromString(testFuncName)
  ret ${std:base:func void #decl #impl}
end

