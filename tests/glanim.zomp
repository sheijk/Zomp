/**

!verify off

* Demonstrates a small animation using OpenGL
!load libglfw.dylib
!load libGLEW.dylib
*/

(include "opengl20.zomp")
(include "glfw.zomp")
(include "glutils.zomp")

(var int windowWidth 400)
(var int windowHeight 300)
  
(runOnceFunc void init () (
  (glfwInit)
  (glfwOpenWindow windowWidth windowHeight 8 8 8 8 16 0 GLFW_WINDOW)
  (glewInit)
  (times 2 (glfwPollEvents))
  ))

(var float angle 0.0)

(func int main () (
  (init)
  
  (glMatrixMode GL_PROJECTION)
  (glLoadIdentity)
  (const double aspect (double.fdiv (int.toDouble windowWidth) (int.toDouble windowHeight)))
  (gluPerspective (int.toDouble 90) aspect 1.0d 100.0d)

  (var float time 0.0)
  
  (var bool running true)
  (while running (
    (assign time  (double.toFloat (glfwGetTime)))
    (assign angle (float.mul time 128.0))
    
    (glClearColor 0. 0. 0. 1.)
    (glClear (int.or GL_COLOR_BUFFER_BIT GL_DEPTH_BUFFER_BIT))

    (glMatrixMode GL_MODELVIEW)
    (glLoadIdentity)
    (glTranslatef 0. 1. -4.)
    (glRotatef angle 0. 1. 0.)

    (drawCoordSys 8.0)

    (glfwSwapBuffers)
    (glfwPollEvents)
    (assign running (bool.not (isPressed GLFW_KEY_ESC)))
    ))

  (drawPausedOverlay)
  (glfwSwapBuffers)

  (glfwPollEvents)
  (ret 0)
  ))

