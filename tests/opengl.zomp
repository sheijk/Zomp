/**
Demonstrates using OpenGL.

Enter this into toplevel to load libs:
!load libglfw.dylib dlltest.dylib

You will need to copy the file libglfw.dylib from the lib/macosx dir
of GLFW 2.6 into the zomp main dir
*/

(include "opengl20.zomp")
(include "glfw.zomp")

(func void glfwSetWindowTitle ((cstring title)))

(func cstring gluErrorString ((GLenum error)))

(func void printGLError () (
  (var int error 0)

  (while ((assign error (glGetError)) (int.notEqual error 0)) (
    (const cstring message (gluErrorString error))
    (printString "OpenGL error ")
    (printInt error)
    (printString ": ")
    (printlnString message)
    ))
  ))

(func void printGlfwVersion () (
  (var int major 0)
  (var int minor 0)
  (var int rev 0)

  (glfwGetVersion (ptr major) (ptr minor) (ptr rev))
  
  (printString "Glfw version: ")
  (printString "major = ") (printInt major)
  (printString ", minor = ") (printInt minor)
  (printString ", rev = ") (printInt rev)
  (println)
  ))

(func bool openWindow () (
  (printlnString "Opening window... ")
  (var int result
    (glfwOpenWindow 400 300 8 8 8 8 16 0 GLFW_WINDOW))
  (const bool succeeded (int.equal result GL_TRUE))

  (if succeeded
    (printlnString "ok")
    (printlnString "failed") )
  (ret succeeded)
  ))

(var bool wasInitialized false)

(func void init () (
  (if wasInitialized
    (printlnString "Already initialized")
  // else
    (
    (glfwInit)
    (openWindow)
    (glfwSetWindowTitle "renderTest")
    (glClear GL_COLOR_BUFFER_BIT)
    (glfwSwapBuffers)
    (printGLError)
    (assign wasInitialized true)
    ))
  ))

(func void clearScreen () (
  (glClearColor 1.0 1.0 1.0 1.0)
  (glClear GL_COLOR_BUFFER_BIT)
  (glfwSwapBuffers)
  (printGLError)
  ))

(func void drawTriangle () (
  (glClearColor 0.0 0.0 0.0 1.0)
  (glClear GL_COLOR_BUFFER_BIT)
  (glTranslatef 0.0 0.0 1.0)
  (glColor4f 1.0 0.5 0.0 1.0)
  (glBegin GL_TRIANGLES)
    (glVertex3f -1.0 0.0 0.0)
    (glVertex3f 0.0 1.0 0.0)
    (glVertex3f 1.0 0.0 0.0)
  (glEnd)
  (glfwSwapBuffers)
  (printGLError)
  ))


