/**
 * A smart struct which automatically defines a printing function
 */

include "libs/libcee"

template macro:fail errormsg astVarName
  ret ${error #errormsg #astVarName}
end

// macro macro:fail errormsg more...
//   code := $${error}
//   ast:addChild code errormsg
//   forEachAstChild arg more
//     ast:addChild code arg
//   end
//   mret ${ret ${#code}}
// end

// template macro:expectForm expr form
//   unless ast:matchesShape(#expr, #form)
//     macro:fail "Expected form" #form in #expr
//   end
// end


func void int:print(int* i)
  printInt *i
end

func void float:print(float* f)
  printFloat *f
end

func void char:print(char* c)
  printChar *c
end

macro pstruct name members
  unless (name.childCount == 0)
    ret ${error "Expected id" #name}
  end

  decl := ast:fromString("type")
  printCode := ast:fromString "opseq"

  ast:addChild decl name

  nameQuoted := cstring:quote name.id
  ast:addChild printCode ${printString #nameQuoted}
  ast:addChild printCode ${printString "("}

  forEachAstChild member members
    if ast:matchesShape(member, ${__ _})
      ast:addChild decl member
      fieldName := ast:child member 1
      fieldType := ast:child member 0
      fieldTypeName := fieldType.id
      fieldPrinterName := fieldTypeName ++ ":print"
      fieldNameQuoted := cstring:quote fieldName.id
      ast:addChild printCode ${printString " "}
      ast:addChild printCode ${printString #fieldNameQuoted}
      ast:addChild printCode ${printString "="}
      ast:addChild printCode ${#fieldPrinterName &(this.#fieldName)}
    else
      macro:fail "Expected 'typeExpr id'" member
    end
  end

  ast:addChild printCode ${printString ")"}

  printerName := name.id ++ ":print"
  printF := ${func void #printerName(#name* this) #printCode}

  ret ${
    #decl
    #printF
  end}
end

pstruct Bar
  char c
  char x
end

pstruct Foo
  int nat
  float real
  Bar b
end

func int main()
  var Foo f
  f.nat = 10
  f.real = 3.7
  f.b.c = 'c'
  f.b.x = 'x'

  Foo:print &f

  ret 0
end

