/**
Generates and shows several SH functions

!verify off

Enter this into toplevel to load libs:
!load libglfw.dylib
!load libGLEW.dylib
!load glut.dylib

You will need to copy the file libglfw.dylib from the lib/macosx dir
of GLFW 2.6 into the zomp main dir

Conventions for function names:
  send* - sending vertex data (to be included in glbegin/end)
  render* - send geometry (outside of glBegin/glEnd)
  draw* - setup material and render
*/

(include "opengl20.zomp")
(include "glfw.zomp")
(include "tests/glutils.zomp")
(include "glut.zomp")

(const int windowWidth 800)
(const int windowHeight 600)
(const float windowAspect 1.0)
(var bool initialized false)

(func bool init () (
  (template assertGLTrue code (
    (if (int.notEqual GL_TRUE #code) (
      (printlnString "Init failed")
      (printAst `#code 0)
      (ret false))
    ())))
  
  (if initialized (ret true) ())

  (assertGLTrue (glfwInit))
  (assertGLTrue (glfwOpenWindow windowWidth windowHeight 8 8 8 8 16 0 GLFW_WINDOW))
  (glewInit)

  //   (glfwSetWindowPos 300 1300) */

  (windowAspect = int.toFloat windowWidth /_f int.toFloat windowHeight)

  (assign initialized true)
  (ret true)
  ))

//------------------------------------------------------------------------------
// utils

(macro compareFunc type name compareF (
  (var cstring typeName (getField type id))

  (var (ptr ast) compareCode compareF)
  (addChilds compareCode `l `r)

  (ret `(func #typeName #name ((#typeName l) (#typeName r)) (
    (var bool cmp #compareCode)
    (branch cmp lgreater rgreater)
    (label lgreater)
    (ret l)
    (label rgreater)
    (ret r)
    )))
  ))


(compareFunc int int:max op>)
(compareFunc int int:min op<)

(compareFunc float float:max op>_f)
(compareFunc float float:min op<_f)

(compareFunc double double:max op>_d)
(compareFunc double double:min op<_d)

(func float fmodf ((float num) (float divisor)))


(macro arrayOf type values ... (
  (uniqueId array "arrayOf")
  
  (var (ptr ast) code `())
  (var int size (getField values childCount))
  (addChild code `(var (ptr #type) #array (malloc #type #size)))

  (var int index 0)
  (forEachAstChild val values (
    (addChild code `(store (ptradd #array #index) #val))
    
    (index = index + 1)
    ))

  (addChild code `(#array))

  (ret code)
  ))

(macro arrayOfFuncs type initCalls ... (
  (uniqueId array "arrayOfFuncs")

  (var (ptr ast) code `())
  (var int size (getField initCalls childCount))
  (addChild code `(var (ptr #type) #array (malloc #type #size)))

  (var int index 0)
  (forEachAstChild call initCalls (
    (ast.replace call "this" `(ptradd #array #index))

    (addChild code call)

    (index = index + 1)
    ))

  (addChild code `(#array))

  (ret code)
  ))

(template array:set array index value (store (ptradd #array #index) #value))
(template array:get array index (load (ptradd #array #index)))
(template op[] array index (load (ptradd #array #index)))

//------------------------------------------------------------------------------
// OpenGL utils

(func void clear ((float r) (float g) (float b) (float a)) (
  (glClearColor r g b a)
  (glClear (int.or GL_COLOR_BUFFER_BIT GL_DEPTH_BUFFER_BIT))
  ))

(func GLint gluBuild2DMipmaps (
  (GLenum target)
  (GLint internalFormat)
  (GLsizei width)
  (GLsizei height)
  (GLenum format)
  (GLenum type)
  ((ptr void) data) ))

/// turns 4 colors values in [0, 255] into an RGBA color suitable for OpenGL
(func int rgbaColor ((int r) (int g) (int b) (int a)) (
  (template clampToByte x (int:max 0 (int:min 255 #x)))
  (const int red (clampToByte r))
  (const int green (clampToByte g))
  (const int blue (clampToByte b))
  (const int alpha (clampToByte a))
  
  (ret (alpha*256*256*256 + blue*256*256 + green*256 + red))
  ))

(func int rgbaColor_f ((float r) (float g) (float b) (float a)) (
  (template mapfi value (float.toInt (#value *_f 255.0)))
  (rgbaColor (mapfi r) (mapfi g) (mapfi b) (mapfi a))
  ))

//------------------------------------------------------------------------------
// vector math

(type float3
  (float x)
  (float y)
  (float z) )
(type float3p (ptr float3))

(macro constructor type fields ... (
  (var cstring funcName (cstring:concat (getField type id) ":init"))
  (var (ptr ast) args `(((ptr #type) this)))
  (var (ptr ast) impl `())

  (forEachAstChild field fields (
    (const int fieldChildCount (getField field childCount))

    (var (ptr ast) fieldType (nullptr ast))
    (var cstring fieldName (nullptr char))
    
    (if (fieldChildCount == 1)
      ((fieldType = (simpleAst (getField field id)))
        (fieldName = getField (astChild field 0) id)
        )
      (// else
        (printlnString "Error: Expected (type name) for fields")
        (printAst field 2) (println)
        )
      )
    
    (addChild args field)

    (addChild impl `(setField this #fieldName #fieldName))
    ))
  
  (var (ptr ast) funcDef `(func void #funcName #args #impl))

  (ret funcDef)  
  ))

(constructor float3 (float x) (float y) (float z))

(func float3p float3:new ((float x) (float y) (float z)) (
  (var float3p this (malloc float3))
  (float3:init this x y z)
  (ret this)
  ))


(macro float3:apply2 this func l r (
  (var astp code `(
    (setField #this x (funcPlaceholder (getField #l x) (getField #r x)))
    (setField #this y (funcPlaceholder (getField #l y) (getField #r y)))
    (setField #this z (funcPlaceholder (getField #l z) (getField #r z)))
    ))

  // workaround because `(#func l r) would turn into (seq funcValue l r)
  (ast:replaceId code "funcPlaceholder" (getField func id))
  
  (ret code)
  ))

(template float3:deriveApplyArgFunc name simpleFunc
  (func void #name ((float3p this) (float3p r)) (
    (float3:apply2 this #simpleFunc this r)
    ))
  )

(float3:deriveApplyArgFunc float3:add op+_f)
(float3:deriveApplyArgFunc float3:sub op-_f)
(float3:deriveApplyArgFunc float3:mul op*_f)
(float3:deriveApplyArgFunc float3:div op/_f)


(func void float3:interpolate ((float3p this) (float t) (float3p a) (float3p b)) (
  (template interpolateField field
    (setField this #field (t *_f getField a #field +_f (1.0 -_f t) *_f getField b #field)))

  (interpolateField x)
  (interpolateField y)
  (interpolateField z)
  ))

(func float float3:lengthSquared ((float3p this)) (
  (const float x (getField this x))
  (const float y (getField this y))
  (const float z (getField this z))
  (x *_f x  +_f  y *_f y  +_f  z *_f z)
  ))

(func float float3:length ((float3p this)) (
  (sqrtf (float3:lengthSquared this))
  ))

(func void float3:normalize ((float3p this)) (
  (const float factor (1.0 /_f float3:length this))
  (setField this x (getField this x *_f factor))
  (setField this y (getField this y *_f factor))
  (setField this z (getField this z *_f factor))
  ))

/// gets an (ptr float3) and a function (float -> float) and applies the function
/// to all elements of the vector
(template float3:apply this func (
  (setField #this x (func (getField #this x)))
  (setField #this y (func (getField #this y)))
  (setField #this z (func (getField #this z)))
  ))


/// t \in [0, 1], 0.0 -> a, 1.0 -> b
(func float float:interpolate ((float t) (float a) (float b)) (
  (t *_f a  +_f  (1.0 -_f t) *_f b)
  ))
  
//------------------------------------------------------------------------------

(func void plot ((int size) ((ptr float) values)) (
  (glBegin GL_LINE_STRIP)

  (const float delta (2.0 /_f int.toFloat size))

  (var float x -1.0)
  (for i 0 size (
    (const float y (array:get values i))
    (glVertex2f x y)
    (x = x +_f delta)
    ))
  
  (glEnd)
  ))

(type RGBA int)

(func RGBA shbase ((int l) (int m) (float longitude) (float latitude)) (
  (const float red (longitude /_f 360.0))
  (const float green (latitude /_f 180.0 +_f 0.5))

  (rgbaColor (float.toInt (red *_f 255.0)) (float.toInt (green *_f 255.0))  0 255)
  ))

(func GLuint makeCubemap ((int size)) (
  (var GLuint texture (makeTexture))

  (const float sizef (int.toFloat size))
  
  (glBindTexture GL_TEXTURE_CUBE_MAP texture)

  (var (ptr int) colors (arrayOf int
    (rgbaColor 255 0 0 255)
    (rgbaColor 0 255 0 255)
    (rgbaColor 0 0 255 255)
    (rgbaColor 0 255 255 255)
    (rgbaColor 255 0 255 255)
    (rgbaColor 255 255 0 255) ))

  (var (ptr float3) edges (arrayOfFuncs float3
    (float3:init this -1. -1. 1.)
    (float3:init this  1. -1. 1.)
    (float3:init this  1.  1. 1.)
    (float3:init this -1.  1. 1.)
    
    (float3:init this -1. -1. -1.)
    (float3:init this  1. -1. -1.)
    (float3:init this  1.  1. -1.)
    (float3:init this -1.  1. -1.)
    ))


  (var (ptr (ptr int)) edgeIndices (arrayOf (ptr int)
    (arrayOf int 1 5 6 2)
    (arrayOf int 4 0 3 7)
    (arrayOf int 3 2 6 7)
    (arrayOf int 4 5 1 0)
    (arrayOf int 0 1 2 3)
    (arrayOf int 5 4 7 6)
    ))
  
  (var (ptr int) pixels (malloc int (size * size)))

  (for sideNum 0 6 (
    (const int target (GL_TEXTURE_CUBE_MAP_POSITIVE_X + sideNum))
    
    (var int c (load colors))

    (var (ptr int) sideEdgeIndices (edgeIndices[sideNum]))
    
    (var (ptr float3) lowerLeft (ptradd edges (sideEdgeIndices[0])))
    (var (ptr float3) lowerRight (ptradd edges (sideEdgeIndices[1])))
    (var (ptr float3) upperRight (ptradd edges (sideEdgeIndices[2])))
    (var (ptr float3) upperLeft (ptradd edges (sideEdgeIndices[3])))

    (var float3p left (new float3 0. 0. 0.))
    (var float3p right (new float3 0. 1. 0.))
    
    (var float3p dir (new float3 0. 0. 0.))
    
    (for y 0 size (
      (var float yUnit (int.toFloat y /_f sizef))
      
      (float3:interpolate left yUnit lowerLeft upperLeft)
      (float3:interpolate right yUnit lowerRight upperRight)
      
      (for x 0 size (
        (float3:interpolate dir (int.toFloat x /_f sizef) right left)
        (float3:normalize dir)
        
        (const int pixelNum (y * size + x))
        (c = rgbaColor_f (getField dir x) (getField dir y) (getField dir z) 1.0)
        /*         (c = shbase 0 0 longitude latitude) */
        (array:set pixels pixelNum c)
        ))
      ))
    
    (gluBuild2DMipmaps target GL_RGBA size size GL_RGBA GL_UNSIGNED_BYTE
      (cast (ptr void) pixels))
    
    (colors = ptradd colors 1)
    ))

  (glBindTexture GL_TEXTURE_CUBE_MAP 0)
  
  (ret texture)
  ))
  
(func GLuint makeCubemapShader () (
  (var GLuint shader (createShader
    "
    varying vec3 texCoord;
    "
    
    "void main(void) {
      gl_Position = ftransform();
      
      vec4 dir = normalize( gl_Vertex );
      texCoord = dir.xyz;
      }
    "
    
    "
    uniform samplerCube texture;
    
    void main(void) {
      gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0) + textureCube( texture, texCoord );
      }
    "))
  (ret shader)
  ))

(template onKey key code ...
  (if (isPressed #key) #code ()))


(type trackball
  /// rotation around y axis (in degrees)
  (float longitude)
  /// rotation around x axis (in degrees)
  (float latitude)
  (float distance)
  )

(func (ptr trackball) trackball:new ((float longitude) (float latitude) (float distance)) (
  (var (ptr trackball) this (malloc trackball))
  (setField this longitude longitude)
  (setField this latitude latitude)
  (setField this distance distance)
  (ret this)
  ))

(func void trackball:update (((ptr trackball) this) (float seconds)) (
  (var float rotationSpeed 90.0)
  (onKey GLFW_KEY_LSHIFT
    (rotationSpeed = rotationSpeed *_f 3.0))

  (template addToField name value
    (setField this #name (getField this #name +_f #value)) )
  
  (onKey GLFW_KEY_LEFT
    (addToField longitude (seconds *_f float.neg rotationSpeed)) )
  (onKey GLFW_KEY_RIGHT
    (addToField longitude (seconds *_f rotationSpeed)) )
  (onKey GLFW_KEY_UP
    (addToField latitude (seconds *_f rotationSpeed)) )
  (onKey GLFW_KEY_DOWN
    (addToField latitude (seconds *_f float.neg rotationSpeed)) )

  (setField this longitude (fmodf (getField this longitude) 360.0))
  (setField this latitude (float:max -90.0 (float:min 90.0 (getField this latitude))))
  ))

(func void trackball:transform (((ptr trackball) this)) (
  (glTranslatef 0. 0. (float.sub 1.0 (getField this distance)))
  (glRotatef (getField this latitude) 1. 0. 0.)
  (glRotatef (getField this longitude) 0. -1. 0.)
  ))


(macro arrayType elementType (
  (const cstring name (cstring:concat "array_" (getField elementType id)))
  (const cstring initName (cstring:concat name ":init"))
  
  (ret `(
    (type #name (int size) ((ptr #elementType) values))
    
    (func void #initName (((ptr #name) this) (int size)) (
      (setField this size size)
      (setField this values (malloc #elementType size))
      ))
    ))
  ))

(arrayType float)
(arrayType array_float)


(template mainloop code ... (
  (var bool abort false)

  (var double startTime (glfwGetTime))
  (var double lastTime startTime)
  
  (while (bool.not abort)
    (if (isPressed GLFW_KEY_ESC)
      (abort = true) ())

    (var double frameTime (glfwGetTime))
    (var double frameDur_d (frameTime -_d lastTime))
    (var float frameDur (double.toFloat frameDur_d))
    (lastTime = frameTime)

    #code
    
    (glfwPollEvents)
    (glfwSwapBuffers)
    )
  ))
  
(func float legendre ((int l) (int m) (float x)) (
  (var float pnm 1.0)

  (if (m > 0)
    (//then
      (var float somx2 (sqrtf( (1.0 -_f x) *_f (1.0 +_f x) )))
      (var float fact 1.0)
      (for i 1 (m+1) (
        (pnm = pnm *_f float.neg fact *_f somx2)
        (fact = fact +_f 2.0)
        ))
      )
    () )

  (if (l == m) (ret pnm) ())

  (var float pnmp1 (x *_f (2.0 *_f int.toFloat m +_f 1.0) *_f pnm))

  (if (l == m + 1) (ret pnmp1) ())
  
  (var float pll 0.0)

  (for ll (m+2) (l+1) (
    (pll = ( (2.0 *_f int.toFloat ll -_f 1.0) *_f x *_f pnmp1
      -_f (int.toFloat(m+ll) -_f 1.0) *_f pnm ) /_f int.toFloat (ll - m) )

    (pnm = pnmp1)
    (pnmp1 = pll)
    ))
    
  (ret pll)
  ))

(func int main () (
  (init)

  (times 10 (glfwPollEvents) )

  (var GLuint cubeShader (makeCubemapShader))
  (var GLuint cubemap (makeCubemap 512))

  (glMatrixMode GL_PROJECTION)
  (glLoadIdentity)
  (gluPerspective 90.0d (float.toDouble windowAspect) 1.0d 500.0d)
  
  (glMatrixMode GL_MODELVIEW)
  (glLoadIdentity)
  (gluLookAt
    10.0d 10.0d 10.0d
    0.0d 0.0d 0.0d
    0.0d 1.0d 0.0d)

  (glEnable GL_DEPTH_TEST)
  (glDepthFunc GL_LEQUAL)


  (var GLuint legendreDL (glGenLists 1))
  (glNewList legendreDL GL_COMPILE)
  
  (const int legendreSize 100)

  (for l 0 3 (
    (for m 0 (l+1) (
      
      (glBegin GL_LINE_STRIP)
      (for i 0 legendreSize (
        (var float x (-1.0 +_f 2.0 *_f (int.toFloat i /_f int.toFloat (legendreSize - 1))))
        (var float y (legendre l m x))
        (glVertex2f x y)
        ))
      (glEnd)
      
      )) // for m
    )) // for l

  (glEndList)

  (var (ptr trackball) cam (new trackball 45.0 10.0 10.0))

  (mainloop
    (trackball:update cam frameDur)

    (glMatrixMode GL_MODELVIEW)
    (glLoadIdentity)
    (trackball:transform cam)
    
    (clear 0. 0. 0. 1.)

    (drawOrientationGrid)

    (glUseProgram cubeShader)
    (glBindTexture GL_TEXTURE_CUBE_MAP cubemap)
    (glutSolidSphere 5.0d 20 20)
    (glBindTexture GL_TEXTURE_CUBE_MAP 0)
    (glUseProgram 0)

    (saveTransforms
      (glRotatef 45.0 0. 1. 0.)
      (glTranslatef 0. 1. (4. +_f 2.))
      (const float scale 1.0)
      (glScalef (2.0 *_f scale) scale scale)
      
      (glColor4f 1. 1. 0. 1.)
      (glCallList legendreDL)

      (glTranslatef 0. 0. (-1.0 /_f 100.0))
      (glBegin GL_LINES)
      (glColor4f 1. 1. 1. 1.)
      (glVertex2f -1. 0.) (glVertex2f 1. 0.)
      (glVertex2f 0. -1.) (glVertex2f 0. 1.)
      (glEnd)

      (glPushAttrib (int.or GL_BLEND GL_DEPTH))
      (glDepthFunc GL_LESS)
      (glEnable GL_BLEND)
      (glBlendFunc GL_SRC_ALPHA GL_ONE_MINUS_SRC_ALPHA)
      (glColor4f 1. 1. 1. (1.0 /_f 4.0))
      (renderQuad 1.0)
      (glPopAttrib)
      )
    )

  (drawPausedOverlay)

  (glDeleteLists legendreDL 1)
  (glDeleteTextures 1 (ptr cubemap))
  (glDeleteShader cubeShader)

  (ret 0)
  ))

(func void makeScreenBlack () (
  (clear 0. 0. 0. 1.)
  (glfwSwapBuffers)
  ))

/* NSBundle* webKitBundle; */
/*     webKitBundle = [NSBundle bundleWithPath:@"/System/Library/Frameworks/WebKit.framework"]; */
/*     if (webKitBundle) { */
/*         _webkitAvailable = [webKitBundle load]; */
/*     } */
  